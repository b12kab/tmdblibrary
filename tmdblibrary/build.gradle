apply plugin: 'com.android.library'
apply plugin: 'de.mannodermaus.android-junit5'

android {
    repositories {
        maven { url "https://jitpack.io" }
        maven { url 'https://repo1.maven.org/maven2' }
    }

    compileSdkVersion 33

    // Move to JUnit 5 - https://github.com/mannodermaus/android-junit5

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33

        //testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // 1) Make sure to use the AndroidJUnitRunner, of a subclass of it. This requires a dependency on androidx.test:runner, too!
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // 2) Connect JUnit 5 to the runner
        testInstrumentationRunnerArguments runnerBuilder: 'de.mannodermaus.junit5.AndroidJUnit5Builder'
    }

    // 3) Java 8 is required
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 4) JUnit 5 will bundle in files with identical paths; exclude them
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/LICENSE*']
        }
        resources {
            excludes += ['META-INF/LICENSE*']
        }
    }

    buildTypes {
        debug {
            // These values come from your personal local.properties Gradle file
            buildConfigField "String", "TMDB_TEST_ID", "\"${project.TMDB_ID}\""
            buildConfigField "String", "TMDB_TEST_GOOD_PSWD", "\"${project.TMDB_GOOD_PSWD}\""
            buildConfigField "String", "TMDB_TEST_BAD_PSWD", "\"${project.TMDB_BAD_PSWD}\""
            buildConfigField "String", "TMDB_TEST_API_KEY",  "\"${project.TMDB_JAVA_LIBRARY_TEST_API_KEY}\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.9.0'

    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.3"))
    // define any required OkHttp artifacts without version
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    implementation 'org.apache.commons:commons-lang3:3.12.0'

    // Force higher level versions of hamcrest in, so earlier addition
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'

    // 5) Jupiter API & Test Runner, if you don't have it already
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"

    // 6) The instrumentation test companion libraries
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.2.2"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.2.2"

    // Assertions
    // https://medium.com/@boonkeat/android-unit-testing-with-junit5-d1b8f9c620b6
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.2"
}